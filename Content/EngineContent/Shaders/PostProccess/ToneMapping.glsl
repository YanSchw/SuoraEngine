#type vertex
#version 330 core

layout(location = 0) in vec3 a_Position;
layout(location = 2) in vec2 a_TexCoord;
out vec2 UV;

void main(void)
{
	gl_Position = vec4(a_Position, 1.0);
	UV = a_TexCoord;
}

#type fragment
#version 330 core

in vec2 UV;
out vec4 out_Color;

uniform sampler2D u_Texture;
uniform int u_TonemapFunction;

vec4 unreal(vec4 x) {
  return x / (x + 0.155) * 1.019;
}

vec4 aces(vec4 x) {
  const float a = 2.51;
  const float b = 0.03;
  const float c = 2.43;
  const float d = 0.59;
  const float e = 0.14;
  return clamp((x * (a * x + b)) / (x * (c * x + d) + e), 0.0, 1.0);
}


// https://github.com/dmnsgn/glsl-tone-map/blob/master/filmic.glsl
vec3 tonemapFilmic(vec3 x) {
  vec3 X = max(vec3(0.0), x - 0.004);
  vec3 result = (X * (6.2 * X + 0.5)) / (X * (6.2 * X + 1.7) + 0.06);
  return pow(result, vec3(2.2));
}

// https://github.com/dmnsgn/glsl-tone-map/blob/master/lottes.glsl
vec3 lottes(vec3 x) {
  const vec3 a = vec3(1.6);
  const vec3 d = vec3(0.977);
  const vec3 hdrMax = vec3(8.0);
  const vec3 midIn = vec3(0.18);
  const vec3 midOut = vec3(0.267);

  const vec3 b =
      (-pow(midIn, a) + pow(hdrMax, a) * midOut) /
      ((pow(hdrMax, a * d) - pow(midIn, a * d)) * midOut);
  const vec3 c =
      (pow(hdrMax, a * d) * pow(midIn, a) - pow(hdrMax, a) * pow(midIn, a * d) * midOut) /
      ((pow(hdrMax, a * d) - pow(midIn, a * d)) * midOut);

  return pow(x, a) / (pow(x, a * d) * b + c);
}

// https://github.com/dmnsgn/glsl-tone-map/blob/master/reinhard.glsl
vec3 reinhard(vec3 x) {
  return x / (1.0 + x);
}
// https://github.com/dmnsgn/glsl-tone-map/blob/master/reinhard2.glsl
vec3 reinhard2(vec3 x) {
  const float L_white = 4.0;

  return (x * (1.0 + x / (L_white * L_white))) / (1.0 + x);
}

// https://github.com/dmnsgn/glsl-tone-map/blob/master/uchimura.glsl
vec3 uchimura(vec3 x, float P, float a, float m, float l, float c, float b) {
  float l0 = ((P - m) * l) / a;
  float L0 = m - m / a;
  float L1 = m + (1.0 - m) / a;
  float S0 = m + l0;
  float S1 = m + a * l0;
  float C2 = (a * P) / (P - S1);
  float CP = -C2 / P;

  vec3 w0 = vec3(1.0 - smoothstep(0.0, m, x));
  vec3 w2 = vec3(step(m + l0, x));
  vec3 w1 = vec3(1.0 - w0 - w2);

  vec3 T = vec3(m * pow(x / m, vec3(c)) + b);
  vec3 S = vec3(P - (P - S1) * exp(CP * (x - S0)));
  vec3 L = vec3(m + a * (x - m));

  return T * w0 + L * w1 + S * w2;
}

vec3 uchimura(vec3 x) {
  const float P = 1.0;  // max display brightness
  const float a = 1.0;  // contrast
  const float m = 0.22; // linear section start
  const float l = 0.4;  // linear section length
  const float c = 1.33; // black
  const float b = 0.0;  // pedestal

  return uchimura(x, P, a, m, l, c, b);
}

// https://github.com/dmnsgn/glsl-tone-map/blob/master/uncharted2.glsl
vec3 uncharted2Tonemap(vec3 x) {
  float A = 0.15;
  float B = 0.50;
  float C = 0.10;
  float D = 0.20;
  float E = 0.02;
  float F = 0.30;
  float W = 11.2;
  return ((x * (A * x + C * B) + D * E) / (x * (A * x + B) + D * F)) - E / F;
}

vec3 uncharted2(vec3 color) {
  const float W = 11.2;
  float exposureBias = 2.0;
  vec3 curr = uncharted2Tonemap(exposureBias * color);
  vec3 whiteScale = 1.0 / uncharted2Tonemap(vec3(W));
  return curr * whiteScale;
}

void main(void)
{
	vec4 color = texture(u_Texture, UV);

	//color = unreal(color);

       if (u_TonemapFunction == 0) color = aces(color);
  else if (u_TonemapFunction == 1) color = unreal(color);
  else if (u_TonemapFunction == 2) color = vec4(tonemapFilmic(color.rgb), 1.0);
  else if (u_TonemapFunction == 3) color = vec4(lottes(color.rgb), 1.0);
  else if (u_TonemapFunction == 4) color = vec4(reinhard(color.rgb), 1.0);
  else if (u_TonemapFunction == 5) color = vec4(reinhard2(color.rgb), 1.0);
  else if (u_TonemapFunction == 6) color = vec4(uchimura(color.rgb), 1.0);
  else if (u_TonemapFunction == 7) color = vec4(uncharted2(color.rgb), 1.0);

	out_Color = color;
}